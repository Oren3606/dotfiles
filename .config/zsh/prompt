# TODO: Some further work may be needed
# Prompt components and colors
# https://en.wikipedia.org/wiki/ANSI_escape_code#8-bit
username=$'\e[38;5;2m%n\e[0m'
at=$'\e[38;5;2m@\e[0m'
hostname=$'\e[38;5;2m%m\e[0m'
time=$'\e[38;5;1m%D{%H:%M}\e[0m'
directory=$'\e[38;5;4m%~\e[0m'
privileges=$'\e[38;5;15m$\e[0m'
newline=$'\n'

# Combine
PROMPT="${transient_element}${username}${at}${hostname} ${time} ${directory}${newline}${privileges} "

###############################
# https://gist.github.com/subnut/3af65306fbecd35fe2dda81f59acf2b2

[[ -c /dev/null ]]  ||  return
zmodload zsh/system ||  return

# Whats left afterwards
TRANSIENT_PROMPT="${privileges} "
# Whats shown currently
function set_prompt {
    PROMPT="${transient_element}${username}${at}${hostname} ${time} ${directory}${newline}${privileges} "
    RPROMPT=""
}

zle -N send-break _transient_prompt_widget-send-break
function _transient_prompt_widget-send-break {
    _transient_prompt_widget-zle-line-finish
    zle .send-break
}

zle -N zle-line-finish _transient_prompt_widget-zle-line-finish
function _transient_prompt_widget-zle-line-finish {
    (( ! _transient_prompt_fd )) && {
        sysopen -r -o cloexec -u _transient_prompt_fd /dev/null
        zle -F $_transient_prompt_fd _transient_prompt_restore_prompt
    }
    zle && PROMPT=$TRANSIENT_PROMPT RPROMPT= zle reset-prompt && zle -R
}

function _transient_prompt_restore_prompt {
    exec {1}>&-
    (( ${+1} )) && zle -F $1
    _transient_prompt_fd=0
    set_prompt
    zle reset-prompt
    zle -R
}

(( ${+precmd_functions} )) || typeset -ga precmd_functions
(( ${#precmd_functions} )) || {
    do_nothing() {true}
    precmd_functions=(do_nothing)
}

precmd_functions+=_transient_prompt_precmd
function _transient_prompt_precmd {
    TRAPINT() { zle && _transient_prompt_widget-zle-line-finish; return $(( 128 + $1 )) }
}

